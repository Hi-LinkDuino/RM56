# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../../appexecfwk.gni")

module_output_path = "bundle_framework/bundlemgrservice"

config("bundlemgr_parse_test_config") {
  include_dirs = [
    "${services_path}/bundlemgr/include",
    "${services_path}/bundlemgr/test/mock/include",
  ]
}

ohos_unittest("BmsBundleParserTest") {
  use_exceptions = true
  module_out_path = module_output_path

  sources = [
    "${services_path}/bundlemgr/src/inner_bundle_info.cpp",
    "${services_path}/bundlemgr/src/inner_bundle_user_info.cpp",
    "${services_path}/bundlemgr/src/pre_install_bundle_info.cpp",
    "bms_bundle_parser_test.cpp",
  ]

  sources += [ "${services_path}/bundlemgr/test/mock/src/accesstoken_kit.cpp" ]

  configs = [
    "${services_path}/bundlemgr/test:bundlemgr_test_config",
    ":bundlemgr_parse_test_config",
  ]
  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  deps = [ "${services_path}/bundlemgr:bundle_parser" ]

  external_deps = [
    "ability_base:want",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
  ]
  defines = []
  if (ability_runtime_enable) {
    external_deps += [ "ability_runtime:ability_manager" ]
    defines += [ "ABILITY_RUNTIME_ENABLE" ]
  }
  if (global_resmgr_enable) {
    defines += [ "GLOBAL_RESMGR_ENABLE" ]
    external_deps += [ "resource_management:global_resmgr" ]
  }
  resource_config_file =
      "${appexecfwk_path}/test/resource/bundlemgrservice/ohos_test.xml"
}

group("unittest") {
  testonly = true
  deps = [ ":BmsBundleParserTest" ]
}

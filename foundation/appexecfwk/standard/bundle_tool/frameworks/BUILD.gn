# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../bundletool.gni")

config("tools_bm_config") {
  include_dirs = [
    "include",
    "${common_path}/log/include",
    "${kits_path}/js/bundlemgr",
    "//utils/system/safwk/native/include",
  ]

  defines = [
    "APP_LOG_TAG = \"BundleTool\"",
    "LOG_DOMAIN = 0xD001120",
  ]
}

ohos_source_set("tools_bm_source_set") {
  sources = [
    "${common_path}/log/src/app_log_wrapper.cpp",
    "${kits_path}/js/bundlemgr/bundle_death_recipient.cpp",
    "src/bundle_command.cpp",
    "src/main.cpp",
    "src/shell_command.cpp",
    "src/status_receiver_impl.cpp",
  ]

  public_configs = [ ":tools_bm_config" ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  external_deps = [
    "ability_base:want",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "os_account:os_account_innerkits",
    "resource_management:global_resmgr",
    "samgr_standard:samgr_proxy",
    "startup_l2:syspara",
    "utils_base:utils",
  ]

  part_name = "bundle_tool"
}

ohos_executable("bm") {
  deps = [ ":tools_bm_source_set" ]

  install_enable = true

  subsystem_name = "appexecfwk"
  part_name = "bundle_tool"
}

group("tools_bm") {
  deps = [ ":bm" ]
}

# Copyright (c) 2020 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../config.gni")

config("samgr_source_public") {
  include_dirs = [
    "../adapter",
    "../registry",
    "//foundation/distributedschedule/samgr_lite/interfaces/kits/registry",
    "//foundation/distributedschedule/samgr_lite/interfaces/kits/samgr",
    "//utils/native/lite/include",
  ]
}

if (ohos_kernel_type == "liteos_m") {
  static_library("samgr_source") {
    sources = [
      "common.c",
      "feature.c",
      "iunknown.c",
      "message.c",
      "service.c",
      "task_manager.c",
    ]

    public_configs = [ ":samgr_source_public" ]
    cflags = [ "-Wall" ]

    public_deps = [
      "//foundation/distributedschedule/samgr_lite/samgr/adapter:samgr_adapter",
    ]

    include_dirs =
        [ "//base/hiviewdfx/hilog_lite/interfaces/native/kits/hilog_lite" ]

    if (enable_ohos_distributedschedule_samgr_lite_rpc_mini) {
      public_deps += [
        "//foundation/communication/ipc/interfaces/innerkits/c/dbinder:dbinder",
      ]
    }
  }
}

if (ohos_kernel_type == "liteos_a" || ohos_kernel_type == "linux") {
  source_set("samgr_source") {
    sources = [
      "common.c",
      "feature.c",
      "iunknown.c",
      "message.c",
      "service.c",
      "task_manager.c",
    ]

    cflags = [
      "-fPIC",
      "-Wall",
    ]

    public_configs = [ ":samgr_source_public" ]

    public_deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//foundation/distributedschedule/samgr_lite/samgr/adapter:samgr_adapter",
    ]

    include_dirs = [ "//third_party/bounds_checking_function/include" ]
  }
}

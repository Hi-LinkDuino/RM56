# Copyright (c) 2020-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/lite/config/component/lite_component.gni")
import("//build/lite/ndk/ndk.gni")
import("//foundation/ace/ace_engine_lite/ace_lite.gni")

declare_args() {
  # player lite implement path, if not the null, vendor could
  # implement player.
  config_ohos_multimedia_media_lite_player_path = ""
}

if (config_ohos_multimedia_media_lite_player_path == "") {
  lite_library("audio_lite_api") {
    if (ohos_kernel_type == "liteos_m") {
      target_type = "static_library"
    } else {
      target_type = "shared_library"
    }

    sources = [
      "src/audio_module.cpp",
      "src/audio_player.cpp",
    ]

    include_dirs = ace_lite_include_dirs
    include_dirs += [
      "//foundation/multimedia/media_lite/interfaces/kits/player_lite/js/builtin/include",
      "//foundation/multimedia/media_lite/interfaces/kits/player_lite",
      "//foundation/multimedia/utils/lite/interfaces/kits",
    ]

    public_deps = [
      "//foundation/multimedia/media_lite/frameworks/player_lite",
      "//foundation/multimedia/utils/lite:media_common",
    ]

    if (ohos_kernel_type == "liteos_m") {
      public_deps += [ "//third_party/bounds_checking_function:libsec_static" ]
    } else {
      public_deps += [ "//third_party/bounds_checking_function:libsec_shared" ]
    }
  }
} else {
  static_library("audio_lite_api") {
    public_deps = [ config_ohos_multimedia_media_lite_player_path ]
  }
}

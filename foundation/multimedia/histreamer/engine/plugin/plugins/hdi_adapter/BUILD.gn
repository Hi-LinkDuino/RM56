# Copyright (c) 2021-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
import("//foundation/multimedia/histreamer/config.gni")

group("plugin_hdi_adapter") {
  deps = []
  if (hst_is_lite_sys) {
    deps += [ ":histreamer_plugin_HdiAuSink" ]
  } else {
    print("plugin_hdi_adapter is not available on standard systems")
  }
}

if (hst_is_lite_sys) {
  import("//build/lite/config/component/lite_component.gni")
  plugin_hdi_adapter_src = [
    "sink/hos_au_sink.cpp",
    "utils/hdi_au_utils.cpp",
  ]
  config("plugin_hdi_adapter_config") {
    include_dirs = [ "//drivers/peripheral/audio/interfaces/include" ]
    ldflags = [ "-lhdi_videodisplayer" ]
  }

  lite_library("histreamer_plugin_HdiAuSink") {
    if (ohos_kernel_type == "liteos_m") {
      target_type = "static_library"
    } else {
      target_type = "shared_library"
    }
    sources = plugin_hdi_adapter_src
    public_configs = [
      ":plugin_hdi_adapter_config",
      "//foundation/multimedia/histreamer:histreamer_presets",
    ]
    public_deps = [
      "//foundation/multimedia/histreamer/engine/foundation:histreamer_foundation",
      "//foundation/multimedia/histreamer/engine/plugin:histreamer_plugin_intf",
      "//foundation/multimedia/histreamer/engine/utils:histreamer_utils",
    ]
  }
} else {
  print("plugin_hdi_adapter is not available on standard systems")
}

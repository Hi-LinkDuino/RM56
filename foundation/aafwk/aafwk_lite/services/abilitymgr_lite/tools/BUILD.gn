# Copyright (c) 2020 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/lite/config/component/lite_component.gni")
import("//build/lite/config/subsystem/aafwk/config.gni")

lite_component("tools_lite") {
  features = [ ":aa" ]
}

generate_notice_file("tools_lite_notice_file") {
  module_name = "tools_lite"
  module_source_dir_list = [
    "//third_party/bounds_checking_function",
    "//third_party/cJSON",
  ]
}

executable("aa") {
  sources = [
    "src/ability_tool.cpp",
    "src/main.cpp",
  ]

  cflags = [ "-Wall" ]
  cflags_cc = cflags

  ldflags = [
    "-lstdc++",
    "-lpthread",
    "-ldl",
  ]

  deps = [
    "${aafwk_lite_path}/frameworks/abilitymgr_lite:aafwk_abilityManager_lite",
    "${appexecfwk_lite_path}/frameworks/bundle_lite:bundle",
    "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
    "//build/lite/config/component/cJSON:cjson_shared",
    "//foundation/communication/ipc_lite:liteipc_adapter",
    "//foundation/distributedschedule/samgr_lite/samgr:samgr",
    "//utils/native/lite/kv_store:kv_store",
  ]

  if (enable_ohos_appexecfwk_feature_ability == true) {
    deps += [
      "//foundation/graphic/surface",
      "//foundation/graphic/ui:lite_ui",
      "//foundation/graphic/utils:lite_graphic_utils",
    ]
  }
  include_dirs = [
    "include",
    "${aafwk_lite_path}/interfaces/kits/ability_lite",
    "${aafwk_lite_path}/interfaces/kits/want_lite",
    "${aafwk_lite_path}/interfaces/innerkits/abilitymgr_lite",
    "${aafwk_lite_path}/frameworks/abilitymgr_lite/include",
    "${aafwk_lite_path}/frameworks/want_lite/include",
    "${appexecfwk_lite_path}/utils/bundle_lite",
    "${appexecfwk_lite_path}/interfaces/kits/bundle_lite",
    "//foundation/communication/ipc_lite/interfaces/kits",
    "//foundation/distributedschedule/samgr_lite/interfaces/kits/registry",
    "//foundation/distributedschedule/samgr_lite/interfaces/kits/samgr",
    "//third_party/bounds_checking_function/include",
    "//third_party/cJSON",
    "//utils/native/lite/include",
  ]

  defines = [ "OHOS_APPEXECFWK_BMS_BUNDLEMANAGER" ]
  output_dir = "$root_out_dir/dev_tools"
}

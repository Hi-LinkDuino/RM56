# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")

module_output_path = "bluetooth/framework_test/"
SUBSYSTEM_DIR = "//foundation/communication"

###############################################################################
#1. intent(c++) get/set test without transport

config("module_private_config") {
  visibility = [ ":*" ]
  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  include_dirs = [
    "mock/include",
    "//third_party/json/include",
    "//commonlibrary/c_utils/base/include",
    "//utils/system/safwk/native/include",
    "//foundation/communication/bluetooth/interfaces/inner_api/include",
    "//foundation/communication/bluetooth/frameworks/inner/common",
  ]
  defines = [
    "BTFW_LOG_TAG = \"bluetooth_test\"",
    "BTFW_LOG_DOMAIN = 0xD000103",
  ]
}

ohos_unittest("btfw_gatt_spp_unit_test") {
  module_out_path = module_output_path

  sources = [
    "gatt/gatt_client_test.cpp",
    "gatt/gatt_descriptor_test.cpp",
    "gatt/gatt_manager_test.cpp",
    "gatt/gatt_server_test.cpp",
    "gatt/gatt_service_test.cpp",
    "spp/spp_test.cpp",
  ]

  configs = [ ":module_private_config" ]

  deps = [
    "$SUBSYSTEM_DIR/bluetooth/frameworks/inner:btframework",
    "$SUBSYSTEM_DIR/bluetooth/services/bluetooth/common:btcommon",
    "$SUBSYSTEM_DIR/bluetooth/services/bluetooth/ipc:btipc_static",
    "//base/global/resource_management/frameworks/resmgr:global_resmgr",
    "//third_party/googletest:gmock_main",
    "//third_party/googletest:gtest_main",
  ]

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
  ]
}

################################################################################
group("unittest") {
  testonly = true

  deps = []

  if (is_phone_product) {
    deps += [ ":btfw_gatt_spp_unit_test" ]
  }
}

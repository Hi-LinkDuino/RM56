# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ace/ace_engine/ace_config.gni")

# Common config for Ability Cross-platform Environment(ACE) source
config("ace_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$ace_root",
    "$ace_root/frameworks",
    "$root_out_dir/ace/framework",
    "//third_party/zlib",
  ]

  if (is_standard_system && !use_mingw_win && !use_mac) {
    include_dirs += [
      "//base/global/resmgr_standard/frameworks/resmgr/include",
      "//base/global/resmgr_standard/interfaces/innerkits/include",
      "$ace_root/adapter/ohos/services/uiservice/include",
    ]
  }
  defines = ace_common_defines

  if (is_wearable_product) {
    defines += ace_wearable_defines
  }

  if (is_ivi_product) {
    defines += ace_ivi_defines
  }

  if (defined(product_name) &&
      (product_name == "rk3566" || product_name == "rk3568")) {
    defines += [ "PRODUCT_RK" ]
  }

  cflags = [
    "-fvisibility=hidden",
    "-fdata-sections",
    "-ffunction-sections",
    "-Os",
  ]

  cflags_cc = [
    "-fvisibility-inlines-hidden",
    "-Os",
  ]

  if (use_mingw_win) {
    cflags_cc += [ "-std=c++17" ]
    defines += [ "_USE_MATH_DEFINES" ]
  }
}

config("ace_test_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$ace_root",
    "$ace_root/frameworks",
    "$root_out_dir/ace/framework",
  ]
  cflags_cc = []
  defines = ace_common_defines

  if (is_standard_system && !use_mingw_win && !use_mac) {
    include_dirs += [
      "//base/global/resmgr_standard/frameworks/resmgr/include",
      "//base/global/resmgr_standard/interfaces/innerkits/include",
    ]

    cflags_cc += [
      "-Wno-thread-safety-attributes",
      "-Wno-thread-safety-analysis",
    ]
    defines += [ "OHOS_STANDARD_SYSTEM" ]
  }

  if (!is_standard_system) {
    defines += [ "ENABLE_NATIVE_VIEW" ]
  }

  defines += [ "OHOS_PLATFORM" ]

  if (is_wearable_product) {
    defines += ace_wearable_defines
  }

  cflags = [
    "-fvisibility=hidden",
    "-fdata-sections",
    "-ffunction-sections",
  ]

  cflags_cc += [ "-fvisibility-inlines-hidden" ]
}

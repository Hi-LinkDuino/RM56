# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

config("libhilog_pub_config") {
  visibility = [ ":*" ]

  include_dirs = [ "include" ]
}

ohos_shared_library("libhilog") {
  public_configs = [ ":libhilog_pub_config" ]

  deps = [
    "//base/hiviewdfx/hilog/adapter:libhilog_os_adapter",
    "//base/hiviewdfx/hilog/frameworks/native:libhilog_source",
  ]

  output_extension = "so"
  install_enable = true
  install_images = [
    "system",
    "updater",
  ]
  part_name = "hilog_native"
  subsystem_name = "hiviewdfx"
}

declare_args() {
  ohos_hilog_base_default_privacy = true
}

config("libhilog_base_cfg") {
  visibility = [ "*:*" ]
  include_dirs = [ "include" ]

  defines = [ "HILOG_DEFAULT_PRIVACY=$ohos_hilog_base_default_privacy" ]
  defines += [
    "__RECV_MSG_WITH_UCRED_",
    "HILOG_PROHIBIT_ALLOCATION",
  ]
}

ohos_static_library("libhilog_base") {
  include_dirs = [
    "//base/hiviewdfx/hilog/frameworks/native",
    "//base/hiviewdfx/hilog/frameworks/native/include",
  ]
  sources = [
    "//base/hiviewdfx/hilog/frameworks/native/hilog_base.cpp",
    "//base/hiviewdfx/hilog/frameworks/native/vsnprintf_s_p.cpp",
  ]

  public_configs = [ ":libhilog_base_cfg" ]
  configs = [ ":libhilog_base_cfg" ]

  subsystem_name = "hiviewdfx"
  part_name = "hilog_native"
}

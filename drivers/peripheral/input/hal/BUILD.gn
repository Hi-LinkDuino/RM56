# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

if (defined(ohos_lite)) {
  config("input_hdi_public_config") {
    visibility = [ ":*" ]
    include_dirs = [
      "include",
      "//drivers/peripheral/input/interfaces/include",
      "//third_party/bounds_checking_function/include",
      "//third_party/FreeBSD/sys/dev/evdev",
    ]
  }

  ohos_shared_library("hdi_input") {
    output_extension = "z.so"
    sources = [
      "src/input_controller.c",
      "src/input_manager.c",
      "src/input_reporter.c",
    ]

    public_configs = [ ":input_hdi_public_config" ]
    include_dirs = []

    deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
    external_deps = [ "device_driver_framework:libhdf_utils" ]
    defines = [ "__USER__" ]

    cflags = [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-fsigned-char",
      "-fno-common",
      "-fno-strict-aliasing",
    ]

    subsystem_name = "hdf"

    install_images = [
      chipset_base_dir,
      "updater",
    ]
    part_name = "input_device_driver"
  }
} else {
  INPUT_ROOT_DIR = "//drivers/peripheral/input"
  config("input_hdi_public_config") {
    visibility = [ ":*" ]
    include_dirs = [
      "$INPUT_ROOT_DIR/interfaces/include",
      "//third_party/FreeBSD/sys/dev/evdev",
    ]
  }

  ohos_shared_library("hdi_input") {
    sources = [
      "src/input_controller.c",
      "src/input_manager.c",
      "src/input_reporter.c",
    ]

    public_configs = [ ":input_hdi_public_config" ]
    include_dirs = [ "$INPUT_ROOT_DIR/hal/include" ]

    defines = [ "__OHOS__USER__" ]

    cflags = [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-fsigned-char",
      "-fno-common",
      "-fno-strict-aliasing",
    ]

    subsystem_name = "hdf"
    if (is_standard_system) {
      external_deps = [
        "device_driver_framework:libhdf_utils",
        "hiviewdfx_hilog_native:libhilog",
        "utils_base:utils",
      ]
    } else {
      external_deps = [ "hilog:libhilog" ]
    }
    install_images = [
      chipset_base_dir,
      "updater",
    ]
    part_name = "input_device_driver"
  }
}
